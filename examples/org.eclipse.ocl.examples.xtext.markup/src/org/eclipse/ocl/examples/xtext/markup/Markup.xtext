/**
 * <copyright>
 *
 * Copyright (c) 2011 E.D.Willink and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     E.D.Willink - initial API and implementation
 *
 * </copyright>
 *
 * $Id$
 */
grammar org.eclipse.ocl.examples.xtext.markup.Markup hidden()

//generate markup "http://www.omg.org/ocl/markup/Markup"
											
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "platform:/resource/org.eclipse.ocl.examples.xtext.markup/model/Markup.ecore"

Markup:
	elements+=MarkupElement*;
	
MarkupElement:
	FontElement
|	NewLineElement
|	TextElement
|	FigElement
|	FigRefElement
|	NullElement
|	OclElement
;

TextElement:
	text+=(ID|WORD|WS)+
;

NewLineElement:
	text=NL
;

FontElement:
	font=('b['|'e[') elements+=MarkupElement* ']'
;

FigElement:
	('fig[' | ('fig:' id=ID '[')) src=STRING ']'
;

FigRefElement:
	'fig-[' ref=[FigElement|ID] ']'
;

NullElement:
	{NullElement} '[' elements+=MarkupElement* ']'
;

OclElement:
	{OclElement} 'ocl[' elements+=MarkupElement* ']'
;

MarkupKeyword:
	'b['
|	'e['
|	'fig['
|	'fig:'
|	'fig-['
|	'ocl['
|	']'
;
//terminal INT returns ecore::EInt: ('0'..'9')+;
terminal ID: ('a'..'z' | 'A'..'Z' | '_') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')*;
terminal WORD: ( '\\' ('b'|'t'|'n'|'f'|'r'|'"'|"'"|'\\'|'<'|'>'|'['|']') | !('\\'|'['|']'|' '|'\t'|'\n'|'\r') )+;
terminal STRING	: '"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'"'|"'"|'\\'|'<'|'>') | !('\\'|'"') )* '"' ; 
terminal TEXT	: "'" ( '\\' ('b'|'t'|'n'|'f'|'r'|'"'|"'"|'\\'|'['|']') | !('\\' | "'" | "[" | "]" | '\n') )* "'" ; 

terminal NL			: ('\n'|'\r') (WS* ('\n'|'\r'))*;
terminal WS			: (' '|'\t')+;

terminal ANY_OTHER: .;

// section2/3/4
// sec-ref
// bullets
// fig-ref
// fig:name + cap
// emphasized-ocl
// normal-ocl